#version 450 core
#extension GL_GOOGLE_include_directive : enable
#extension GL_ARB_separate_shader_objects : enable

layout(local_size_x = 256) in;

#include "physics.inc"
#include "particle.inc"

layout(set=0, binding=0, std430) coherent buffer particleBuffer {
    particle_t particles [];
};

layout( push_constant ) uniform constants{
    float t;
    float dt;
};

void main() {
    uint id = gl_GlobalInvocationID.x;

    if (id >= particles.length()) {
        return;
    }

    vec3 position = particles[id].position;
    float lifetime = particles[id].lifetime;
    vec3 velocity = particles[id].velocity;

    if (lifetime > dt) {
        lifetime -= dt;
    } else {
        lifetime = 0.0f;
    }

    const float fading = 1.0f / (1.0f + friction);

    position = position + velocity * dt;
    velocity = velocity * fading + vec3(0.0f, -g, 0.0f) * dt;

    particles[id].position = position;
    particles[id].lifetime = lifetime;
    particles[id].velocity = velocity;
}
