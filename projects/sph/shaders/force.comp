#version 450 core
#extension GL_ARB_separate_shader_objects : enable

const float PI = 3.1415926535897932384626433832795;

#define h 0.045
#define gravity 9.81
#define mass 0.1
#define viscosity 1500

layout(local_size_x = 256) in;

struct Particle
{
    vec3 position;
    float padding;
    vec3 velocity;
    float density;
    vec3 force;
    float pressure;
    
};

layout(std430, binding = 1) readonly buffer buffer_inParticle
{
    Particle inParticle[];
};

layout(std430, binding = 0) writeonly buffer buffer_outParticle
{
    Particle outParticle[];
};

layout( push_constant ) uniform constants{
    float deltaTime;
    float particleCount;
};

float spiky(float r)    
{
    return 15 / (PI * pow(h, 6)) * pow((h-r), 3) * int(0<=r && r<=h);
}

float laplacian(float r)
{
    return 45 / (PI * pow(h,6)) * (h - r) * int(0<=r && r<=h);
}

vec3 pressureForce = vec3(0, 0, 0);
vec3 viscosityForce = vec3(0, 0, 0); 
vec3 externalForce = vec3(0, 0, 0);

void main() {
    uint id = gl_GlobalInvocationID.x;

    if(id >= int(particleCount))
    {
        return;
    }

    externalForce = vec3(0, inParticle[id].density * gravity, 0);

    for(uint i = 0; i < inParticle.length(); i++)  
    {
        vec3 dir = inParticle[id].position - inParticle[i].position;
        float dist = length(dir);
        pressureForce += mass * -(inParticle[id].pressure + inParticle[i].pressure)/(2.f * inParticle[i].density) * spiky(dist) * normalize(dir);
        viscosityForce += mass * (inParticle[id].velocity - inParticle[i].velocity)/inParticle[i].density * laplacian(dist);
    }
    viscosityForce *= viscosity;

    outParticle[id].force = externalForce + pressureForce + viscosityForce;
    outParticle[id].density = inParticle[id].density;
    outParticle[id].pressure = inParticle[id].pressure;
    outParticle[id].position = inParticle[id].position;
    outParticle[id].velocity = inParticle[id].velocity;
}
