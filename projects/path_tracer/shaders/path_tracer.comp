#version 450 core
#extension GL_ARB_separate_shader_objects : enable

const float pi      	= 3.1415926535897932384626433832795;
const float hitBias 	= 0.0001;   // used to offset hits to avoid self intersection
const float denomMin 	= 0.001;

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

struct Material {
    vec3 	emission;
	float 	ks;		// specular percentage
    vec3 	albedo;
    float 	r;		// roughness
};

struct Sphere{
    vec3 	center;
    float 	radius;
    int 	materialIndex;
	float 	padding[3];
};

struct Plane{
	vec3 	center;
	int 	materialIndex;
	vec3 	N;
	float 	padding1;
	vec2 	extent;
	vec2 	padding2;
};

layout(std430, binding = 0) buffer spheres{
    Sphere inSpheres[];
};

layout(std430, binding = 1) buffer planes{
    Plane inPlanes[];
};

layout(std430, binding = 2) buffer materials{
    Material inMaterials[];
};

layout(set=0, binding = 3, rgba32f) uniform image2D outImage;

layout( push_constant ) uniform constants{
    mat4 	viewToWorld;
	vec3 	skyColor;
    int 	sphereCount;
	int 	planeCount;
	int 	frameIndex;
};

// ---- Intersection functions ----

struct Ray{
	vec3 origin;
	vec3 direction;
};

struct Intersection{
    bool 		hit;
	float 		distance;
    vec3 		pos;
    vec3 		N;
    Material 	material;
};

Intersection raySphereIntersect(Ray ray, Sphere sphere){

	Intersection intersection;
	intersection.hit = false;
	
    vec3 	L 	= sphere.center - ray.origin;
    float 	tca = dot(L, ray.direction);
    float 	d2 	= dot(L, L) - tca * tca;
    if (d2 > sphere.radius * sphere.radius){
        return intersection;
    }
    float thc = float(sqrt(sphere.radius * sphere.radius - d2));
    float t0 = tca - thc;
    float t1 = tca + thc;
    if (t0 < 0) {
        t0 = t1;
    }
    if (t0 < 0){
        return intersection;
    }
	
	intersection.hit 		= true;
	intersection.distance 	= t0;
	intersection.pos        = ray.origin + ray.direction * intersection.distance;
	intersection.N          = normalize(intersection.pos - sphere.center);
	intersection.material 	= inMaterials[sphere.materialIndex];
	
    return intersection;
}

struct Basis{
	vec3 right;
	vec3 up;
	vec3 forward;
};

Basis buildBasisAroundNormal(vec3 N){
	Basis 	basis;
	basis.up 		= N;
	basis.right 	= abs(basis.up.x) < 0.99 ?  vec3(1, 0, 0) : vec3(0, 0, 1);
	basis.forward 	= normalize(cross(basis.up, basis.right));
	basis.right 	= cross(basis.up, basis.forward);
	return basis;
}

// see: https://www.scratchapixel.com/lessons/3d-basic-rendering/minimal-ray-tracer-rendering-simple-shapes/ray-plane-and-ray-disk-intersection
Intersection rayPlaneIntersect(Ray ray, Plane plane){

	Intersection intersection;
	intersection.hit = false;

	vec3 	toPlane = plane.center - ray.origin;
	float 	denom 	= dot(ray.direction, plane.N);
	if(abs(denom) < 0.001)
		return intersection;
		
	intersection.distance = dot(toPlane, plane.N) / denom;
	
	if(intersection.distance < 0)
		return intersection;
	
	intersection.pos 				= ray.origin + ray.direction * intersection.distance;
	
	vec3 	centerToIntersection	= intersection.pos - plane.center;
	Basis 	planeBasis 				= buildBasisAroundNormal(plane.N);
	float 	projectedRight			= dot(centerToIntersection, planeBasis.right);
	float 	projectedUp				= dot(centerToIntersection, planeBasis.forward);

	intersection.hit 		= abs(projectedRight) <= plane.extent.x && abs(projectedUp) <= plane.extent.y;
	intersection.N 			= plane.N;
	intersection.material 	= inMaterials[plane.materialIndex];
	
	return intersection;
}

Intersection sceneIntersect(Ray ray) {
    float minDistance = 100000;  // lets start with something big
    
    Intersection intersection;
    intersection.hit = false;
    
    for (int i = 0; i < sphereCount; i++) {
		Intersection sphereIntersection = raySphereIntersect(ray, inSpheres[i]);
        if (sphereIntersection.hit && sphereIntersection.distance < minDistance) {            
            intersection 	= sphereIntersection;
			minDistance 	= intersection.distance;
        }
    }
	for (int i = 0; i < planeCount; i++){
		Intersection planeIntersection = rayPlaneIntersect(ray, inPlanes[i]);
        if (planeIntersection.hit && planeIntersection.distance < minDistance) {
            intersection 	= planeIntersection;
			minDistance 	= intersection.distance;
        }
	}
    return intersection;
}

vec3 biasHitPosition(vec3 hitPos, vec3 rayDirection, vec3 N){
    // return hitPos + N * hitBias; // works as long as no refraction/transmission is used and camera is outside sphere
    return hitPos + sign(dot(rayDirection, N)) * N * hitBias;
}

// ---- noise/hash functions for pseudorandom variables ----

// extremelearning.com.au/unreasonable-effectiveness-of-quasirandom-sequences
vec2 r2Sequence(uint n){
	n = n % 42000;
	const float g = 1.32471795724474602596;
	return fract(vec2(
		n /  g,
		n / (g*g)));
}

// random() and helpers from: https://www.shadertoy.com/view/XlycWh
float g_seed = 0;

uint base_hash(uvec2 p) {
    p = 1103515245U*((p >> 1U)^(p.yx));
    uint h32 = 1103515245U*((p.x)^(p.y>>3U));
    return h32^(h32 >> 16);
}

vec2 hash2(inout float seed) {
    uint n = base_hash(floatBitsToUint(vec2(seed+=.1,seed+=.1)));
    uvec2 rz = uvec2(n, n*48271U);
    return vec2(rz.xy & uvec2(0x7fffffffU))/float(0x7fffffff);
}

void initRandom(ivec2 coord){
	g_seed = float(base_hash(coord)/float(0xffffffffU)+frameIndex);
}

vec2 random(){
	return hash2(g_seed);
}

// ---- shading ----

vec3 lambertBRDF(vec3 albedo){
	return albedo / pi;
}

vec3 computeDiffuseBRDF(Material material){
    return lambertBRDF(material.albedo);
}

float distributionGGX(float r, float NoH){
	float r2 	= r*r;
	float denom = pi * pow(NoH*NoH * (r2-1) + 1, 2);
	return r2 / max(denom, denomMin);
}

float geometryGGXSmith(float r, float NoL){
	float r2 	= r*r;
	float denom = NoL + sqrt(r2 + (1-r2) * NoL*NoL);
	return 2 * NoL / max(denom, denomMin);
}

float geometryGGX(float r, float NoV, float NoL){
	return geometryGGXSmith(r, NoV) * geometryGGXSmith(r, NoL);
}

vec3 fresnelSchlick(vec3 f0, float NoH){
	return f0 + (1 - f0) * pow(1 - NoH, 5);
}

vec3 computeSpecularBRDF(vec3 f0, float r, float NoV, float NoL, float NoH){
	float 	denom 	= 4 * NoV * NoL;
	float 	D 		= distributionGGX(r, NoH);
	float 	G 		= geometryGGX(r, NoV, NoL);
	vec3	F 		= fresnelSchlick(f0, NoH);
	return D * F * G / max(denom, denomMin);
}

// ---- pathtracing and main ----

// distributions: https://link.springer.com/content/pdf/10.1007/978-1-4842-4427-2_16.pdf
float cosineDistributionPDF(float NoL){
	return NoL / pi;
}

vec3 sampleCosineDistribution(vec2 xi){
	float phi = 2 * pi * xi.y;
	return vec3(
		sqrt(xi.x) * cos(phi),
		sqrt(1 - xi.x),
		sqrt(xi.x) * sin(phi));
}

float uniformDistributionPDF(){
	return 1.f / (2 * pi);
}

vec3 sampleUniformDistribution(vec2 xi){
	float phi = 2 * pi * xi.y;
	return vec3(
		sqrt(xi.x) * cos(phi),
		1 - xi.x,
		sqrt(xi.x) * sin(phi));
}

float ggxDistributionPDF(float r, float NoH){
	return distributionGGX(r, NoH) * NoH;
}

float ggxDistributionPDFReflected(float r, float NoH, float NoV){
	float jacobian = 0.25 / max(NoV, denomMin);
	return ggxDistributionPDF(r, NoH) * jacobian;
}

vec3 sampleGGXDistribution(vec2 xi, float r){
	float phi 		= 2 * pi * xi.y;
	float cosTheta 	= sqrt((1 - xi.x) / ((r*r - 1) * xi.x + 1));
	float sinTheta	= sqrt(1 - cosTheta*cosTheta);
	return vec3(
		cos(phi) * sinTheta,
		cosTheta,
		sin(phi) * sinTheta);
}

vec3 sampleTangentToWorldSpace(vec3 tangentSpaceSample, vec3 N){
	Basis tangentBasis = buildBasisAroundNormal(N);
	return
		tangentBasis.right		* tangentSpaceSample.x +
		tangentBasis.up			* tangentSpaceSample.y +
		tangentBasis.forward 	* tangentSpaceSample.z;
}

vec3 castRay(Ray ray) {
    
    vec3   	throughput 	= vec3(1);
    vec3	color   	= vec3(0);
    
	const int maxDepth = 10;
    for(int i = 0; i < maxDepth; i++){

        Intersection intersection = sceneIntersect(ray);
        
        vec3 hitLighting 	= vec3(0);
        vec3 brdf 			= vec3(1);
		
		// V is where the ray came from and will lead back to the camera (over multiple bounces)
		vec3 	V 				= -normalize(ray.direction);
		vec3 	R 				= reflect(-V, intersection.N);
		float 	NoV				= max(dot(intersection.N, V), 0);
		
		intersection.material.r *= intersection.material.r;	// remapping for perceuptual linearity
		intersection.material.r = max(intersection.material.r, 0.01);
		
		float 	kd 				= 1 - intersection.material.ks;
		bool 	sampleDiffuse 	= random().x < kd;
		
		vec3 	sampleTangentSpace;
		float 	pdf;
		if(sampleDiffuse){
			sampleTangentSpace 	= sampleCosineDistribution(random());
			ray.direction   	= sampleTangentToWorldSpace(sampleTangentSpace, intersection.N);
			
			float NoL 			= max(dot(intersection.N, ray.direction), 0);
			pdf 				= cosineDistributionPDF(NoL);
		}
		else{			
			#define IMPORTANCE
			
			#ifdef IMPORTANCE
			sampleTangentSpace 	= sampleGGXDistribution(random(), intersection.material.r);
			ray.direction   	= sampleTangentToWorldSpace(sampleTangentSpace, R);		
			vec3 	L 			= normalize(ray.direction);
			pdf 				= ggxDistributionPDFReflected(intersection.material.r, max(sampleTangentSpace.y, 0.01), max(dot(intersection.N, V), 0.01));
			#else
			sampleTangentSpace 	= sampleUniformDistribution(random());
			ray.direction   	= sampleTangentToWorldSpace(sampleTangentSpace, intersection.N);		
			pdf 				= uniformDistributionPDF();
			#endif
		}
        
        ray.origin				= biasHitPosition(intersection.pos, ray.direction, intersection.N);
		
		// L is where the ray is going, as that is the direction where light will from
		vec3 	L 			= normalize(ray.direction);
		vec3 	H			= normalize(L + V);
		
        float 	NoL 		= max(dot(intersection.N, L), 0);
		float	NoH			= max(dot(intersection.N, H), 0);
		
        if(intersection.hit){
            vec3 	diffuseBRDF 	= computeDiffuseBRDF(intersection.material);

			vec3 	f0  			= vec3(0.04);
			vec3 	specularBRDF 	= computeSpecularBRDF(f0, intersection.material.r, NoV, NoL, NoH);
			brdf 					= mix(diffuseBRDF, specularBRDF, intersection.material.ks);
			
			
			hitLighting = intersection.material.emission * max(sign(NoV), 0);	// objects only emit in direction of normal
        }
        else{
            hitLighting = skyColor;
        }
        
        color       	+= hitLighting * throughput;
        throughput  	*= brdf * NoL / max(pdf, denomMin);
        
        if(!intersection.hit)
            break;
    }

    return color;
}

// coord must be in pixel coordinates, but already shifted to pixel center
vec3 computeCameraRay(vec2 coord){

    ivec2 outImageRes   = imageSize(outImage);
    float fovDegree     = 45;
    float fov           = fovDegree * pi / 180;
    
    vec2 uv     		= coord / vec2(outImageRes);
    vec2 ndc    		= 2 * uv - 1;
    
    float tanFovHalf    = tan(fov / 2.f);
    float aspectRatio   = outImageRes.x / float(outImageRes.y);
    float x             =  ndc.x * tanFovHalf * aspectRatio;
    float y             = -ndc.y * tanFovHalf;
    
    // view direction goes through pixel on image plane with z=1
    vec3 directionViewSpace     = normalize(vec3(x, y, 1));
    vec3 directionWorldSpace    = mat3(viewToWorld) * directionViewSpace;
    return directionWorldSpace;
}

void main(){
    ivec2 	coord     = ivec2(gl_GlobalInvocationID.xy);
	vec2 	pixelSize = 1.f / coord;
	initRandom(coord);
	
	Ray cameraRay;
    cameraRay.origin  	= viewToWorld[3].xyz;
	vec2 coordCentered 	= coord + 0.5;
	
	vec3 color = vec3(0);
	
	const int samplesPerPixel = 1;
	for(int i = 0; i < samplesPerPixel; i++){
		vec2 jitter 		= r2Sequence(i + frameIndex) - 0.5;
		cameraRay.direction = computeCameraRay(coordCentered + jitter);
		color 				+= castRay(cameraRay);
	}
	color /= samplesPerPixel;
		
	vec4 final = vec4(color, 1);
	
	// occasional NaNs in reflection, should be fixed properly
	if(any(isnan(color)))
		final = vec4(0);
	
    imageStore(outImage, coord, final);
}