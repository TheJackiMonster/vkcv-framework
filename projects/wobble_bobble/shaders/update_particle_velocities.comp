#version 450
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_control_flow_attributes : enable

layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

#include "particle.inc"

layout(set=0, binding=0, std430) buffer particleBuffer {
    Particle particles [];
};

layout(set=0, binding=1) uniform texture3D gridImage;
layout(set=0, binding=2) uniform texture3D gridOldImage;
layout(set=0, binding=3) uniform sampler gridSampler;

void main()	{
    memoryBarrierBuffer();
    memoryBarrierImage();

    if (gl_GlobalInvocationID.x < particles.length()) {
        ParticleMinimal minimal = particles[gl_GlobalInvocationID.x].minimal;

        ivec3 gridResolution = textureSize(sampler3D(gridImage, gridSampler), 0);
        ivec3 gridWindow = ivec3(minimal.size * 2.0f * gridResolution);

        vec3 velocity_pic = vec3(0.0f);
        vec3 velocity_flip = vec3(minimal.velocity);

        int i, j, k;

        for (i = -gridWindow.x; i <= gridWindow.x; i++) {
            for (j = -gridWindow.y; j <= gridWindow.y; j++) {
                for (k = -gridWindow.z; k <= gridWindow.z; k++) {
                    vec3 offset = vec3(i, j, k) / gridResolution;
                    vec3 voxel = minimal.position + offset;

                    if (length(offset) < minimal.size * 2.0f) {
                        vec4 gridSample = texture(sampler3D(gridImage, gridSampler), voxel);
                        vec4 gridOldSample = texture(sampler3D(gridOldImage, gridSampler), voxel);

                        float weight = voxel_particle_weight(voxel, minimal);

                        velocity_pic += gridSample.xyz * weight;
                        velocity_flip += (gridSample.xyz - gridOldSample.xyz) * weight;
                    }
                }
            }
        }

        particles[gl_GlobalInvocationID.x].minimal.velocity = mix(velocity_pic, velocity_flip, 0.95f);
    }

    memoryBarrierBuffer();
}